# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Projects

##############################
####++++ Load scripts ++++####
##############################
# load aliases
if [[ -a ~/.alias ]]
then
  source ~/.alias
fi

# load localrc
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi
# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# load nvm-automation
if which nvm >/dev/null && [[ -a ~/.dotfiles/zsh/.nvm-automation.zsh ]]
then
  source ~/.dotfiles/zsh/.nvm-automation.zsh
fi

####--- Load scripts ---####


##############################
####++++ autocomplete ++++####
##############################
# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit -u
compinit -u

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
####---- autocomplete ----####


################################
####++++ better history ++++####
################################
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^[[A" up-line-or-beginning-search # Up
bindkey "^[[B" down-line-or-beginning-search # Down
####---- better history ----####